#Область ПрограммныйИнтерфейс

// Заполняет структуру параметров значениями по умолчанию.
//
// Параметры:
//  ПараметрыОбработчикаОжидания - Структура - заполняется значениями по умолчанию.
//
Процедура ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания) Экспорт
	
	ПараметрыОбработчикаОжидания = Новый Структура;
	ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	
КонецПроцедуры

// Заполняет структуру параметров новыми расчетными значениями.
//
// Параметры:
//  ПараметрыОбработчикаОжидания - Структура - заполняется расчетными значениями.
//
Процедура ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания) Экспорт
	
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал
		* ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	
КонецПроцедуры

// Процедура управляет состояние поля табличного документа.
//
// Параметры:
//  ОтображениеСостояния    - ПолеФормы - поле формы с видом ПолеТабличногоДокумента,
//                                        для которого необходимо установить состояние.
//  Состояние               - Строка    - задает вид состояния.
//
Процедура УстановитьСостояниеПоляТабличногоДокумента(ОтображениеСостояния, Состояние = "НеИспользовать") Экспорт
	
	Если ТипЗнч(ОтображениеСостояния) <> Тип("Надпись") Тогда
		ВызватьИсключение(НСтр("ru='Недопустимое значение параметра (параметр номер ''1'')'"));
	КонецЕсли;
	
	ОтображениеСостояния.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Выпуклая, 3);
	
	Если СтрСравнить(Состояние, "НеИспользовать") = 0 Тогда
		
		ОтображениеСостояния.Видимость = Ложь;
		ОтображениеСостояния.Картинка = Новый Картинка;
		ОтображениеСостояния.Заголовок = "";
		
	ИначеЕсли СтрСравнить(Состояние, "НеАктуальность") = 0 Тогда
		
		ОтображениеСостояния.Видимость = Истина;
		ОтображениеСостояния.Картинка = Новый Картинка;
		ОтображениеСостояния.Заголовок =
			НСтр("ru='Отчет не сформирован. Нажмите ""Сформировать"" для получения отчета.'");
		
	ИначеЕсли СтрСравнить(Состояние, "ФормированиеОтчета") = 0 Тогда
		
		ОтображениеСостояния.Видимость = Истина;
		ОтображениеСостояния.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
		ОтображениеСостояния.Заголовок = Символы.Таб + НСтр("ru='Отчет формируется...'");
		
	Иначе
		ВызватьИсключение(НСтр("ru='Недопустимое значение параметра (параметр номер ''2'')'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти